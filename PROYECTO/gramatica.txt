programa -> funciones

funciones -> funcion funciones
funciones -> ε

funcion -> tipo ID funcion_rest
funcion -> MAIN LPAREN RPAREN bloque
funcion_rest -> inicializacion SEMI
funcion_rest -> LPAREN parametros RPAREN bloque

parametros -> parametro parametros_rest
parametros -> ε

parametros_rest -> COMMA parametro parametros_rest
parametros_rest -> ε

parametro -> tipo ID

bloque -> LBRACE instrucciones RBRACE

instrucciones -> instruccion instrucciones
instrucciones -> ε

instruccion -> declaracion SEMI
instruccion -> asignacion SEMI
instruccion -> If
instruccion -> While
instruccion -> For
instruccion -> Return
instruccion -> Print

declaracion -> tipo ID inicializacion 

inicializacion -> EQUALS exp 
inicializacion -> ε

asignacion -> ID EQUALS exp 

If -> IF LPAREN exp RPAREN bloque ELSE

Else -> ELSE bloque
Else -> ε

While -> WHILE LPAREN exp RPAREN bloque

For -> FOR LPAREN asignacion SEMI exp SEMI asignacion RPAREN bloque

Return -> RETURN exp_opt SEMI

exp_opt -> exp
exp_opt -> ε

Print -> PRINT LPAREN exp RPAREN SEMI

tipo -> INT
tipo -> FLOAT
tipo -> BOOL
tipo -> STRING
tipo -> VOID

exp -> E

E -> C E_rest

E_rest -> OR C E_rest
E_rest -> ε

C -> R C_rest

C_rest -> AND R C_rest
C_rest -> ε

R -> T R_rest

R_rest -> EQ T R_rest
R_rest -> NE T R_rest
R_rest -> LT T R_rest
R_rest -> GT T R_rest
R_rest -> LE T R_rest
R_rest -> GE T R_rest
R_rest -> ε

T -> F T_rest

T_rest -> PLUS F T_rest
T_rest -> MINUS F T_rest
T_rest -> ε

F -> A F_rest

F_rest -> TIMES A F_rest
F_rest -> DIVIDE A F_rest
F_rest -> MOD A F_rest
F_rest -> ε

A -> LPAREN exp RPAREN
A -> ID llamada_func
A -> INT_NUM
A -> TRUE
A -> FALSE

llamada_func -> LPAREN lista_args RPAREN
llamada_func -> ε

lista_args -> exp lista_args_rest
lista_args -> ε

lista_args_rest -> COMMA exp lista_args_rest
lista_args_rest -> ε